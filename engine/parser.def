start
  = headline
    subtitle?
    description
    steps
    conclusion?
    tags?

headline "headline starting with ="
  = "=" _ string ___

subtitle "subtitle starting with =="
  = !blockmarker "==" _ string ___
  
description "description surrounded by ===="
  = blockmarker ___
    descriptionlines
  	blockmarker ___ 

descriptionlines
  = descriptionline+ { return { "descriptionlines": text()}; }
  
descriptionline
  = !blockmarker string __
 
steps
	= step+

step
  = (
      blockmarker ___
      stepinner
      steptextafterlines
      blockmarker __
    )
  / stepinner
  
stepinner
  = !blockmarker steptextlines?
    "[step]" ___
    stepstitle?
    "--" ___
    steplines
    "--" __

blockmarker "===="
  = "===="

stepstitle
  = "==" _ steptitle __
  
steptitle
  = [^\r\n] + { return { "steptitle": text()};}

steptextlines
  = steptextline* { return { "steptextlines": text()}; }

steptextline
  = !"[step]" string __
  
steptextafterlines
  = steptextafterline* { return { "steptextafterlines": text()}; }

steptextafterline
  = !blockmarker !"[step]" string __
 
steplines
  = stepline+ { return { "steplines": text()}; }
  
stepline
  = function __

function "function call"
  = [^\r\n(]+[(][^\r\n)]*[)] { return text(); }

conclusion "conclusion surrounded by ===="
	= blockmarker ___
    conclusionlines
  	blockmarker __ 
    
conclusionlines
  = conclusionline+ { return { "conclusionlines": text()}; }

conclusionline
  = !blockmarker string __ 

tags
  = "[tags]" ___
    "--" ___
    taglines
    "--" __

taglines
  = tagline+ { return { "taglines": text()}; }

tagline
  = !"--" string __ 

string "string"
  = [^\r\n]+ { return text(); }

_ "whitespace"
  = [ \t]*
 
__ "linebreak"
  = [ \t\n\r]*
  
___ "linebreak"
  = [ \t\n\r]+
